/* frameHoldSelected.hsl
 * Freeze frame marker data in the selected range(s).  This will propogate
 * the position of all selected markers from the current time.
 */

// markers to hold
string $markers[] = `getModules -selected -type Marker` ;
int $numMarkers = `getCount $markers` ;

// marker positions to propogate
vector $sourcePos[ $numMarkers ] ;

int $i, $j, $k ;
// Collect Data from this Frame
for( $j=0; $j<$numMarkers; $j+=1 ){
	// backup
	$sourcePos[$j]= `getVectorProperty $markers[$j] "Translation"` ;
}

// ranges to hold
int $ranges[] = `getSelectedTimeRanges` ;
int $numRanges = `getCount $ranges` ;

// work through the ranges
int $startF, $endF ;
for( $i=0; $i<$numRanges; $i+=2 ){
	// get start & end of this range
	$startF = $ranges[$i  ] ;
	$endF   = $ranges[$i+1] ;
	// work through frames of this range
	for( $j=$startF; $j<$endF; $j+=1 ){
		setTime( $j ) ;
		// for every marker
		for( $k=0; $k<$numMarkers; $k+=1 ){
			setKey "Translation" $sourcePos[$k] -onMod $markers[$k] -t $j ;
		}
	}
}
