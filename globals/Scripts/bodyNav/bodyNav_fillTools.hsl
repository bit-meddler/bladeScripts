/* bodyNav_fillTools.hsl
 *
 * All because you can't have a 2 command macro on a button...
 */


// inputs
string	$mode ;
input		$mode "DELFILL" -discrete "NONE" "SPLINE" "DELFILL" "COPY" "RIGID" "SEED" "KLAB" "KSOL" "HOLDF" "HOLDB" ;

// get SplineMax
int $splineMax = 12 ;
if( `getGlobalVarExists "splineMax"` ){
	$splineMax = `getGlobalIntVar "splineMax"` ;
}

switch( $mode ){
	case "NONE":
		// DFA
		break ;
	case "SPLINE":
		fillGaps -ranges -primaryOnly -maxGapWidth $splineMax ;
		break ;
	case "DELFILL":
		cutKeys -ranges ;
		fillGaps -ranges -primaryOnly -maxGapWidth $splineMax ;
		break ;
	case "COPY":
		copyPattern ;
		break ;
	case "RIGID":
		fillGaps -rigid -ranges ;
		break ;
	case "SEED":
		fillGaps -rigid -ranges -useCurrentFrame ;
		break ;
	case "KLAB":
		fillGaps -ranges -primaryOnly -labelingConstraints ;
		break ;
	case "KSOL":
		// Only works for SINGLE constraints
		fillGaps -ranges -primaryOnly -solvingConstraints ;
		/* TODO: average all constraints the Primary Marker Influences
		 *       'copyPattern' this average location.
		 */
		break ;
	case "HOLDF":
		// clear Range Selection
		selectRange -r 0 100000 ;
		int $start, $end ;
		$start = `getTime` ;
		$start += 1 ; // This frame is the seed
		$end = `getAnimEnd` ;
		selectRange $start $end ;
		// select Markers
		bodyNav_selectionTools -mode "ALL" ;
		// Freeze!
		frameHoldSelected ;
		break ;
	case "HOLDB":
		// clear Range Selection
		selectRange -r 0 100000 ;
		int $start, $end ;
		$start = `getAnimStart` ;
		$end = `getTime` ;
		$end -= 1 ; // This frame is the seed
		selectRange $start $end ;
		// select Markers
		bodyNav_selectionTools -mode "ALL" ;
		// Freeze!
		frameHoldSelected ;
		break ;
}