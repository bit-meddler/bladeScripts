/* bodyNav_getGap.hsl
 *
 * Get the Biggest or smallest Gap in the Primary marker
 */
// inputs
string	$dir ;
input		$dir "S" -discrete "<" ">" "N" "P" "S" ;

// Get head/tail over-eat
int $in = 0 ;
int $out = 0 ;
if( `getGlobalVarExists "trimIn"` ){
		$in = `getGlobalIntVar "trimIn"` ;
}
if( `getGlobalVarExists "trimOut"` ){
		$out = `getGlobalIntVar "trimOut"` ;
}
// clear Range Selection
selectRange 0 10000000 -r ;
int $gapList[] ;
int $len ;

// get gap
switch( $dir ){
	case ">":
	case "<":
		// get Gaplist of Primary Marker
		string $mods[] = `getModules -primary` ;
		$gapList = `getGaps $mods[0] -any -sort -noFeedback` ;
		$len = getCount( $gapList ) ;
		if( $len == 0 ){
			break ;
		}
		// Biggest = $len -2, smallest = 0
		int $idx = -1 ;
		if( $dir==">" ) {
			$idx = $len - 2 ;
		} else if( $dir=="<" ) {
			$idx = 0 ;
		}
		// sanity
		if( $idx<0 ){
			return ;
		}
		// add tails
		$in  += $gapList[$idx] ;
		$out += $gapList[$idx+1] ;
		selectRange $in $out ;
		$out -= $in ;
		$out = $out / 2 ;
		$in += $out ;
		setTime $in ;
		break ;
	case "N":
		findGap -selectGapRange $in $out -frameGraph -any -primaryOnly -noFeedback;
		break ;
	case "P":
		findGap -b -selectGapRange $in $out -frameGraph -any -primaryOnly -noFeedback;
		break ;
	case "S":
		// Gap Survey
		// for selected markers
		string $mods[] = `getModules -selected -type Marker` ;
		int $num = getCount( $mods ) ;

		int $i, $big ;
		int $count = 0 ;
		int $max = 0 ;

		for( $i=0; $i<$num; $i+=1 ) {
			$gapList = `getGaps $mods[$i] -any -sort -noFeedback` ;
			$len = getCount( $gapList ) ;
			$count += ($len/2) ;
			if( $len>1 ){
				$big = $gapList[$len - 1] - $gapList[$len - 2] ;
				if( $big>$max ) {
					$max = $big ;
				}
			}
		}
		print( string( $count ) + " Gaps found on " + string( $num ) + " Markers.  Largest Gap is " + string( $max ) ) ;
		return ;
		
}
$gapList = `getSelectedTimeRanges -noFeedback` ;
$len = getCount( $gapList ) ;
if( $len == 0 ) {
	print "No Gaps Found!" ;
} else {
	print( "Gap found" ) ;
	CameraView_frameSelected ;
}